#beg_vs

#version 460 core

out vec2 sent_uv;

const vec2 k_verts[3] = vec2[](
    vec2(-1.0, -1.0),
    vec2( 3.0, -1.0),
    vec2(-1.0,  3.0)
);

void main()
{
    vec2 pos = k_verts[gl_VertexID];
    sent_uv = pos * 0.5 + 0.5;
    gl_Position = vec4(pos, 0.0, 1.0);
}

#end_vs

#beg_fs

#version 460 core

in vec2 sent_uv;

uniform mat4 u_inv_view;
uniform mat4 u_inv_proj;
uniform vec3 u_cam_pos;

out vec4 final_color;

vec3 GetRayDir(vec2 uv)
{
    vec4 clip = vec4(uv * 2.0 - 1.0, 1.0, 1.0);
    vec4 view = u_inv_proj * clip;
    view = vec4(view.xy, -1.0, 0.0);
    return normalize((u_inv_view * view).xyz);
}

bool RayPlane(vec3 ro, vec3 rd, out vec3 hit)
{
    float denom = rd.y;
    if (abs(denom) < 1e-5)
    {
        return false;
    }

    float t = -ro.y / denom;
    if (t < 0.0)
    {
        return false;
    }

    hit = ro + rd * t;
    return true;
}

float Grid(vec2 coord, float scale)
{
    vec2 g = abs(fract(coord / scale - 0.5) - 0.5) / fwidth(coord);
    float line = min(g.x, g.y);
    return 1.0 - clamp(line, 0.0, 1.0);
}

void main()
{
    vec3 ro = u_cam_pos;
    vec3 rd = GetRayDir(sent_uv);

    vec3 p;
    if (!RayPlane(ro, rd, p))
    {
        discard;
    }

    // Grid settings
    float minorScale = 0.1;
    float majorScale = 1.0;
    float minor = Grid(p.xz, minorScale);
    float major = Grid(p.xz, majorScale);

    // Colors
    vec3 minorColor = vec3(0.25);
    vec3 majorColor = vec3(0.45);
    vec3 color = minor * minorColor + major * majorColor;

    // X axis
    if (abs(p.z) < 0.02)
    {
        color = vec3(1.0, 0.2, 0.2);
    }

    // Y axis
    if (abs(p.x) < 0.02)
    {
        color = vec3(0.2, 0.4, 1.0);
    }

    // Distance fade
    float dist = length(p - u_cam_pos);
    float fade = exp(-dist * 0.02);
    fade = clamp(fade, 0.0, 1.0);

    final_color = vec4(color, fade);
}

#end_fs
